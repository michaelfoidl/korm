apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'idea'
//apply plugin: 'at.michaelfoidl.korm'

sourceSets {
    main {
        java.srcDir "${buildDir.absolutePath}/generated/source/kaptKotlin/main"
        java.srcDir "src/main/generated"
    }
    testUtils {
        java.srcDir 'src/testUtils/kotlin'

        compileClasspath += sourceSets.main.output
        compileClasspath += configurations.testUtilsCompile

        runtimeClasspath += output
        runtimeClasspath += compileClasspath
        runtimeClasspath += configurations.testUtilsRuntime
    }
    test {
        java.srcDir 'src/test/kotlin'

        compileClasspath += sourceSets.main.output
        compileClasspath += sourceSets.testUtils.output
        compileClasspath += configurations.testUtilsCompile

        runtimeClasspath += output
        runtimeClasspath += compileClasspath
        runtimeClasspath += configurations.testUtilsRuntime
    }
}

dependencies {
    implementation kotlin_core
    implementation exposed

    testImplementation junit_api
    testRuntimeOnly junit_engine
    testImplementation kluent
    testImplementation project(':testUtils')

    compileOnly project(':annotations')
    kapt project(':processor')
    compile project(':core')
}

configurations {
    testUtilsCompile.extendsFrom compile
    testUtilsRuntime.extendsFrom runtime, testUtilsCompile
}

idea {
    module {
        testSourceDirs += sourceSets.testUtils.kotlin.srcDirs
        scopes.TEST.plus += [configurations.testUtilsCompile, configurations.testUtilsRuntime]
        generatedSourceDirs += sourceSets.main.java.srcDirs
    }
}

test {
    onlyIf {
        false
    }
}

task integrationTest(type: Test) {
    description = "Runs the integration tests (located in src/test/...)."
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}

task cleanTestTmp(type: Delete) {
    doFirst {
        delete "$buildDir/tmp/test"
    }
}

integrationTest.dependsOn(cleanTestTmp)

