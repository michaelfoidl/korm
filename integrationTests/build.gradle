apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'idea'
//apply plugin: 'at.michaelfoidl.korm'

sourceSets {
    main {
        java.srcDir "${buildDir.absolutePath}/korm"
    }
    generated {
        java.srcDir 'src/generated/kotlin'

        compileClasspath += sourceSets.main.output
        compileClasspath += configurations.generatedCompile

        runtimeClasspath += output
        runtimeClasspath += compileClasspath
        runtimeClasspath += configurations.generatedRuntime
    }
    testUtils {
        java.srcDir 'src/testUtils/kotlin'

        compileClasspath += sourceSets.main.output
        compileClasspath += configurations.testUtilsCompile

        runtimeClasspath += output
        runtimeClasspath += compileClasspath
        runtimeClasspath += configurations.testUtilsRuntime
    }
    test {
        java.srcDir 'src/test/kotlin'

        compileClasspath += sourceSets.main.output
        compileClasspath += sourceSets.testUtils.output
        compileClasspath += sourceSets.generated.output
        compileClasspath += configurations.testUtilsCompile
        compileClasspath += configurations.generatedCompile

        runtimeClasspath += output
        runtimeClasspath += compileClasspath
        runtimeClasspath += configurations.testUtilsRuntime
        runtimeClasspath += configurations.generatedRuntime
    }
}

dependencies {
    implementation kotlin_core
    implementation exposed

    testImplementation junit_api
    testRuntimeOnly junit_engine
    testImplementation kluent
    testImplementation project(':testUtils')

    compileOnly project(':annotations')
    kapt project(':processor')
    compile project(':core')
}

configurations {
    testUtilsCompile.extendsFrom compile
    testUtilsRuntime.extendsFrom runtime, testUtilsCompile
    generatedCompile.extendsFrom compile
    generatedRuntime.extendsFrom runtime, generatedCompile
}

idea {
    module {
        testSourceDirs += sourceSets.testUtils.kotlin.srcDirs
        scopes.TEST.plus += [configurations.testUtilsCompile, configurations.testUtilsRuntime]
        generatedSourceDirs += sourceSets.generated.java.srcDirs
    }
}

test {
    onlyIf {
        false
    }
}

task integrationTest(type: Test) {
    description = "Runs the integration tests (located in src/test/...)."
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}

task cleanTestTmp(type: Delete) {
    doFirst {
        delete "$buildDir/tmp/test"
    }
}

integrationTest.dependsOn(cleanTestTmp)

kapt {
    arguments {
        arg("kapt.korm.rootPackage", "at.michaelfoidl.korm.integrationTests.generated")
        arg("kapt.korm.migrationPackage", "migrations")
        arg("kapt.korm.srcDir", "integrationTests/src/generated/kotlin")
        arg("kapt.korm.buildDir", "integrationTests/build/korm")
        arg("kapt.korm.databaseName", "Database")
        arg("kapt.korm.databaseVersion", 1)
        arg("kapt.korm.username", "")
        arg("kapt.korm.password", "")
    }
}

